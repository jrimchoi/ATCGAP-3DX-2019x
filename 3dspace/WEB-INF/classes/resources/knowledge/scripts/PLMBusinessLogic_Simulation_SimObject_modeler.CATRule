Let Prefix=""
Let incAttr=""
Let Operation=""
Let CopyFrom(SIMObjSimulationObjectGeneric)
Let CopyFromId=""
Let CopyFromVN=""
Let BufferStart=""
Let BufferEnd=""
Let AttrSize=0
Let Reference(PLMEntity)
Let New_PLMExternalID=""
Let UniqueKey=""
Let Title=""
Let ObjectCustoType(STRING)
Let IDNumber(STRING)

Operation=Parameters->GetAttributeString("OperationId")

if (Parameters->HasAttribute("IncSessionId") == true) {
      incAttr = ToString(Parameters->GetAttributeInteger("IncSessionId"))
}

ObjectCustoType ="SIMObjSimulationObjectGeneric"

/* Does not exist in V2
if (Parameters->HasAttribute("CustoType") == true){
     set ObjectCustoType = Parameters->GetAttributeString("CustoType") 
*/

IDNumber=STAutoNumber(ObjectCustoType,6)

if (Operation=="ImportAsNew" )
{
	/* ImportAsNew */
	
	if (Parameters->HasAttribute("IdCloningString") == true)
	{
		Prefix = Parameters->GetAttributeString("IdCloningString")
	}
	if (Prefix == "" ) 
	{
	  Prefix = Parameters->GetAttributeString("IdString")
	}
	if ( Parameters->HasAttribute("CopyFrom") == true)
	{
	 Set CopyFrom = Parameters->GetAttributeObject("CopyFrom")
	 CopyFromId = CopyFrom->GetAttributeString("PLM_ExternalID") 
	 CopyFromVN =CopyFrom->GetAttributeString("V_Name") 
	}
	New_PLMExternalID  = Prefix + CopyFromId 
	Title = Prefix + CopyFromVN

	ThisObject.V_description = "Simulation imported as New"
}
else if ( Operation=="Cloning" )
{
	/* cloning */
	
	Prefix = ""
	if (Parameters->HasAttribute("IdCloningString") == true)
	{
	 Prefix = Parameters->GetAttributeString("IdCloningString")
	}
	   
	if ( Parameters->HasAttribute("CopyFrom") == true)
	{
	 Set CopyFrom = Parameters->GetAttributeObject("CopyFrom")
	 CopyFromId = CopyFrom->GetAttributeString("PLM_ExternalID") 
	 CopyFromVN =CopyFrom->GetAttributeString("V_Name") 
	}
	if ( Prefix <> "" )
	{
	  New_PLMExternalID  =  Prefix  + CopyFromId + "-" +incAttr
	  Title = Prefix  + CopyFromVN + "-" +incAttr
	}
	else
	{
	  New_PLMExternalID  =  "Clone-"  + CopyFromId + "-"+ incAttr
	  Title = "Clone-"  + CopyFromVN + "-"+ incAttr
	}

	ThisObject.V_description = "Simulation cloned"
}
 
else
{
  /* new */ 
  
  if (Parameters->HasAttribute("IdString") == true)
  {
      Prefix = Parameters->GetAttributeString("IdString")
      if (Prefix == NULL) Prefix = ""
  }

  if (Parameters->HasAttribute("CoupledRef") == true)
  {
    Set Reference = Parameters ->GetAttributeObject("CoupledRef")  
  } 


  if (Parameters->HasAttribute("IncSessionId") == true) 
  {
      if ( Reference == NULL ) 
      {
	    UniqueKey =  GetUniqueKey( Parameters->GetAttributeString("CustoType"))
       /* Adding Unique Server Key to Assuming PLM_ExternalID is part of ThisObject 'Identifier Set' */
       New_PLMExternalID = "sim" + IDNumber
	   Title = Prefix + "Simulation" + IDNumber
      }
      else 
      {
	   UniqueKey =  GetUniqueKey( Parameters->GetAttributeString("CustoType"))
       /* Adding Unique Server Key to Assuming PLM_ExternalID is part of ThisObject 'Identifier Set' */
       New_PLMExternalID = Reference.PLM_ExternalID +  "sim" +  IDNumber
	   Title = Prefix + "Simulation" + IDNumber
      }
      ThisObject.V_description = "SimulationObject created by " + Parameters.UserID
  }
}


AttrSize=  New_PLMExternalID.Length() 
if ( AttrSize> 60 ) 
{
  BufferStart = New_PLMExternalID.Extract(0,8) 
  BufferEnd = New_PLMExternalID.Extract( AttrSize- 49, 49 )
  New_PLMExternalID = BufferStart + "..." + BufferEnd
} 

if ( New_PLMExternalID <> "" )
{
	ThisObject.PLM_ExternalID  = New_PLMExternalID
}

if ( Title <> "" )
{
	ThisObject.V_Name  = Title 
}
